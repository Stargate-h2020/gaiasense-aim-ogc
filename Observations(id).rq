#+ summary: "Retrieves all the information about specific ILIAD Observations"
#+ description: "NOTE: make sure response content type is application/json"
#+ endpoint: "https://www.foodie-cloud.org/sparql?default-graph-uri=http://w3id.org/iliad/dataaset/jf-pilot-sample"
#+ tags:
#+   - json
#+ method: GET
#+ endpoint_in_url: False
#+ transform: {
#+   "@iot.id": "?id",
#+   "$anchor": "@iot.id",
#+   "@iot.selfLink": "?id_link",
#+   "phenomenonTime": "?phenomenonTime",
#+   "resultTime": "?resultTime",
#+   "result": {
#+        "densityOfJF": "?densityOfJF",
#+        "quantityOfJF": "?quantityOfJF",
#+        "sampleSizeValue": "?sampleSizeValue",
#+        "speciesScientificName": "?scientificName",
#+        "wormsConcept": "?wormsConcept",
#+        "stingByJF": "?stingByJF",
#+        "beachedJF": "?beachedJF",
#+   },
#+   "Datastream@iot.navigationLink": "?datastream_link",
#+   "FeatureOfInterest@iot.navigationLink": "?feature_link",
#+ }

PREFIX iliad-api: <https://w3id.org/iliad/api>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sosa:  <http://www.w3.org/ns/sosa/>
PREFIX oim-jf:  <http://w3id.org/iliad/oim/jellyfish#> 
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?s ?id ?id_link ?resultTime ?phenomenonTime ?densityOfJF ?quantityOfJF ?sampleSizeValue ?scientificName ?wormsConcept ?stingByJF ?beachedJF ?datastream_link ?feature_link
WHERE {
    ?col a sosa:ObservationCollection ;
        sosa:resultTime ?resultTime ;
	    sosa:phenomenonTime ?phenomenonTime ; 
        sosa:hasMember ?s .
    ?s a sosa:Observation ;
		sosa:hasResult ?result .
		OPTIONAL {
    ?result oim-jf:densityOfJF ?densityOfJF ;
        oim-jf:quantityOfJF ?quantityOfJF ; 
		dwc:sampleSizeValue ?sampleSizeValue ;
		dwc:scientificName ?scientificName ;
		oim-jf:wormsConcept ?wormsConcept ;
        oim-jf:stingByJF ?stingByJF ;
	    oim-jf:beachedJF ?beachedJF .
	  }
    FILTER (strEnds(STR(?s), CONCAT('/',?_id)))	
    BIND (REPLACE(STR(?s), "^.*/([^/]*)$", "$1") as ?id)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Observations(', ?_id,')')) AS ?id_link)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Observations(', ?_id,')/Datastream')) AS ?datastream_link)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Observations(', ?_id,')/FeatureOfInterest')) AS ?feature_link)
}
