#+ summary: "Retrieves all the information about ILIAD Observations for a specific FeatureOfInterest"
#+ description: "NOTE: make sure response content type is application/json"
#+ endpoint: "https://www.foodie-cloud.org/sparql?default-graph-uri=http://w3id.org/iliad/dataaset/jf-pilot-sample"
#+ tags:
#+   - json
#+ method: GET
#+ pagination: 100
#+ endpoint_in_url: False
#+ transform: {
#+   "description": "?info",
#+   "$anchor": "description",
#+   "@iot.count": "?total",
#+   "@iot.nextLink": "?next_link",
#+   "value": {
#+     "@iot.id": "?id",
#+     "@iot.selfLink": "?id_link",
#+     "phenomenonTime": "?phenomenonTime",
#+     "resultTime": "?resultTime",
#+     "result": {
#+        "densityOfJF": "?densityOfJF",
#+        "quantityOfJF": "?quantityOfJF",
#+        "sampleSizeValue": "?sampleSizeValue",
#+        "speciesScientificName": "?scientificName",
#+        "wormsConcept": "?wormsConcept",
#+        "stingByJF": "?stingByJF",
#+        "beachedJF": "?beachedJF",
#+     },
#+     "Datastream@iot.navigationLink": "?datastream_link",
#+     "FeatureOfInterest@iot.navigationLink": "?feature_link",
#+   }
#+ }

PREFIX iliad-api: <https://w3id.org/iliad/api>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sosa:  <http://www.w3.org/ns/sosa/>
PREFIX oim-jf:  <http://w3id.org/iliad/oim/jellyfish#> 
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?info ?id ?id_link ?resultTime ?phenomenonTime ?densityOfJF ?quantityOfJF ?sampleSizeValue ?scientificName ?wormsConcept ?stingByJF ?beachedJF ?datastream_link ?feature_link ?total ?next_link
WHERE {
    ?col a sosa:ObservationCollection ;
      sosa:resultTime ?resultTime ;
			sosa:phenomenonTime ?phenomenonTime ; 
      sosa:hasFeatureOfInterest ?foi_URI ;
      sosa:hasMember ?s .
    ?s a sosa:Observation ;
			sosa:hasResult ?result .
    OPTIONAL {
      ?result oim-jf:densityOfJF ?densityOfJF ;
        oim-jf:quantityOfJF ?quantityOfJF ; 
			  dwc:sampleSizeValue ?sampleSizeValue ;
			  dwc:scientificName ?scientificName ;
			  oim-jf:wormsConcept ?wormsConcept ;
        oim-jf:stingByJF ?stingByJF ;
	      oim-jf:beachedJF ?beachedJF .
    }
	BIND ("OGC SensorThings API - Observations" as ?info)	  
    BIND (REPLACE(STR(?s), "^.*/([^/]*)$", "$1") as ?id)
    BIND (REPLACE(STR(?foi_URI), "^.*/([^/]*)$", "$1") as ?foi)
    OPTIONAL { ?s ?p ?_foi }
    FILTER (?foi= ?_foi)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Observations(', ?id,')')) AS ?id_link)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Observations(', ?id,')/Datastream')) AS ?datastream_link)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Observations(', ?id,')/FeautureOfInterest')) AS ?feature_link)
    {
       SELECT (count(*) as ?total) 
       WHERE { 
        ?col a sosa:ObservationCollection ;
          sosa:hasFeatureOfInterest ?foi_URI ;
          sosa:hasMember ?s .
        ?s a sosa:Observation ;
			    sosa:hasResult ?result .
        BIND (REPLACE(STR(?foi_URI), "^.*/([^/]*)$", "$1") as ?foi2)
        OPTIONAL { ?s ?p ?_foi }
        FILTER (?foi2= ?_foi)
      }
    }
    OPTIONAL { ?s ?p ?_page } 
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Datastreams(',?sensor,'-',?property,')/Observations?$top=100&$skip=', (xsd:integer(?_page)+1)*100)) AS ?next_link)
}
